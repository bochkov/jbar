plugins {
    id 'java'
    id 'application'
    id 'idea'
    id 'com.github.ben-manes.versions' version '0.52.0'
}

ext {
    platformArgs = []
    if (System.getProperty("os.name").contains("Windows")) {
        swtPlatform = 'win32.win32.x86_64'
    } else if (System.getProperty("os.name").contains("Mac OS")) {
        swtPlatform = 'cocoa.macosx.x86_64'
        platformArgs += ["-XstartOnFirstThread", "-Xdock:name=JBar"]
    } else {
        swtPlatform = 'linux.linux.x86_64'
    }
}

group = 'com.sergeybochkov'
version = '2.0'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.eclipse.platform:org.eclipse.swt.${swtPlatform}:3.129.0") {
        transitive = false
    }
    implementation 'net.sourceforge.barbecue:barbecue:1.5-beta1'
    implementation 'commons-io:commons-io:2.19.0'

    implementation 'ch.qos.logback:logback-classic:1.5.18'

    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.12.2'
    implementation 'org.slf4j:slf4j-api:2.0.17'
    compileOnly 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'
    testCompileOnly 'org.projectlombok:lombok:1.18.38'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.38'
}

test {
    useJUnitPlatform()
}

application {
    mainClass = 'com.sergeybochkov.jbar.Application'
    applicationDefaultJvmArgs = ["-Xmx64m", "-Dfile.encoding=UTF-8"] + platformArgs
}

tasks.register('macApp') {
    dependsOn 'build'
    doLast {
        def outDir = "$buildDir/macApp" as File
        def appDir = "$outDir/JBar.app" as File
        delete outDir
        project.file(appDir).mkdirs()
        copy {
            from tarTree(distTar.archiveFile)
            into "$appDir/Contents/MacOS"
        }
        copy {
            from 'src/main/resources/images/barcode.icns'
            into "$appDir/Contents/Resources"
        }
        copy {
            from 'src/main/resources/Info.plist'
            into "$appDir/Contents"
            filter { line -> line.replace('{{ root }}', distTar.getArchiveBaseName().get() + "-" + distTar.getArchiveVersion().get()) }
        }
        copy {
            from 'src/main/resources/PkgInfo'
            into "$appDir/Contents"
        }
    }
}

tasks.named('dependencyUpdates').configure {
    rejectVersionIf {
        return ['.*-rc\\d+', '.*-alpha\\d+', '.*-m\\d+'].any { m -> 
            it.candidate.version.toLowerCase().matches(m) 
        }
    }
}
